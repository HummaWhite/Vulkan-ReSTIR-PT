#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_nonuniform_qualifier : enable

#include "ray_layouts.glsl"
#include "camera.glsl"
#include "gbuffer_util.glsl"
#include "light_sampling.glsl"
#include "material.glsl"

layout(location = ClosestHitPayloadLocation) rayPayloadEXT Intersection rtIsec;
layout(location = ShadowPayloadLocation) rayPayloadEXT bool rtShadowed;

void main() {
    const int MaxTracingDepth = 15;
    uvec2 index = gl_LaunchIDEXT.xy;
    vec2 uv = (vec2(index) + 0.5) / vec2(gl_LaunchSizeEXT.xy);

    vec4 depthNormal = texture(uDepthNormal, uv);
    float depth = depthNormal.x;
    vec3 norm = depthNormal.yzw;

    if (depth == 0.0) {
        imageStore(uIndirectOutput, ivec2(index), vec4(vec3(0.0), 1.0));
        return;
    }
    uvec2 albedoMatId = texelFetch(uAlbedoMatId, ivec2(index), 0).rg;
    vec3 albedo = unpackAlbedo(albedoMatId.x);
    int matMeshId = int(albedoMatId.y);
    int matId = matMeshId >> 16;
    
    Ray ray = pinholeCameraSampleRay(uCamera, vec2(uv.x, 1.0 - uv.y), vec2(0));
    uint rng = makeSeed(uCamera.seed + gl_LaunchIDEXT.x, gl_LaunchIDEXT.y);

    vec3 radiance = vec3(0.0);
    vec3 throughput = vec3(1.0);
    vec3 lastPos;
    vec3 wo = -ray.dir;

    SurfaceInfo surf;
    surf.pos = ray.ori + ray.dir * (depth - 1e-4);
    surf.norm = norm;
    surf.albedo = albedo;
    surf.isLight = false;

    Material mat = uMaterials[matId];
    BSDFSample s;

#pragma unroll
    for (int bounce = 0; bounce < MaxTracingDepth; bounce++) {
        if (bounce > 0) {
            traceRayEXT(
                /* AS, flags, masks    */ uTLAS, gl_RayFlagsOpaqueEXT, 0xff,
                /* SBT offset & stride */ 0, 0,
                /* miss index          */ 0,
                /* ray attribs         */ ray.ori, MinRayDistance, ray.dir, MaxRayDistance,
                /* payload location    */ ClosestHitPayloadLocation
            );

            if (!IntersectionIsValid(rtIsec)) {
                break;
            }
            loadSurfaceInfo(rtIsec, surf);
            mat = uMaterials[surf.matIndex];
        }

        if (surf.isLight) {
            float cosTheta = -dot(ray.dir, surf.norm);

             if (/* cosTheta > 0 */ true) {
                float weight = 1.0;

                if (bounce > 0 && !isSampleTypeDelta(s.type)) {
                    float dist = length(surf.pos - lastPos);
                    float sumPower = uLightSampleTable[0].prob;
                    float lightPdf = luminance(surf.albedo) / sumPower * dist * dist / cosTheta;
                    weight = MISWeight(s.pdf, lightPdf);
                }
                radiance += surf.albedo * weight * throughput;
            }
            break;
        }

        if (/* sample direct lighting */ bounce > 0 && !isBSDFDelta(mat)) {
            const uint shadowRayFlags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT;

            vec3 lightRadiance, lightDir;
            float lightDist, lightPdf;

            rtShadowed = true;
            lightRadiance = sampleLight(surf.pos, lightDir, lightDist, lightPdf, rng);

            traceRayEXT(
                /* AS, flags, masks    */ uTLAS, shadowRayFlags, 0xff,
                /* SBT offset & stride */ 0, 0,
                /* miss index          */ 1,
                /* ray attribs         */ surf.pos, MinRayDistance, lightDir, lightDist - 1e-4,
                /* payload location    */ ShadowPayloadLocation
            );

            if (!rtShadowed && lightPdf > 1e-6) {
                float bsdfPdf = absDot(surf.norm, lightDir) * PiInv;
                float weight = MISWeight(lightPdf, bsdfPdf);
                radiance += lightRadiance * evalBSDF(mat, surf.albedo, surf.norm, wo, lightDir) * satDot(surf.norm, lightDir) / lightPdf * weight * throughput;
            }
        }
        if (/* russian roulette */ bounce > 4) {
            float pdfTerminate = max(1.0 - luminance(throughput), 0);

            if (sample1f(rng) < pdfTerminate) {
                break;
            }
            throughput /= (1.0 - pdfTerminate);
        }

        if (!sampleBSDF(mat, surf.albedo, surf.norm, wo, sample3f(rng), s) || s.pdf < 1e-6) {
            break;
        }

        float cosTheta = isSampleTypeDelta(s.type) ? 1.0 : absDot(surf.norm, s.wi);
        throughput *= s.bsdf * cosTheta / s.pdf;
        lastPos = surf.pos;
        
        wo = -ray.dir;
        ray.dir = s.wi;
        ray.ori = surf.pos + ray.dir * 1e-4;
    }
    radiance = fixRadiance(radiance);
    float frameCount = float(uCamera.frameIndex & CameraFrameIndexMask);
    vec3 accumulatedRadaince = imageLoad(uIndirectOutput, ivec2(index)).rgb;
    accumulatedRadaince = (accumulatedRadaince * frameCount + radiance) / (frameCount + 1.0);

    imageStore(uIndirectOutput, ivec2(index), vec4(accumulatedRadaince, 1.0));
}