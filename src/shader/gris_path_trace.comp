#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_ray_query : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_nonuniform_qualifier : enable

#include "HostDevice.h"

layout(local_size_x = RayQueryBlockSizeX, local_size_y = RayQueryBlockSizeY) in;

#include "ray_query.glsl"
#include "camera.glsl"
#include "gbuffer_util.glsl"
#include "light_sampling.glsl"
#include "material.glsl"
#include "gris_reservoir.glsl"

const uint Reconnection = 0;
const uint Hybrid = 1;

layout(push_constant) uniform _PushConstant {
    GRISSettings uSettings;
};

void main() {
    const int MaxTracingDepth = 15;
    uvec2 threadIdx = gl_GlobalInvocationID.xy;
    uvec2 launchDim = uCamera.filmSize;

    if (threadIdx.x >= launchDim.x || threadIdx.y >= launchDim.y) {
        return;
    }
    vec2 uv = (vec2(threadIdx) + 0.5) / vec2(launchDim);

    vec4 depthNormal = texture(uDepthNormal, uv);
    float depth = depthNormal.x;
    vec3 norm = depthNormal.yzw;

    if (depth == 0.0) {
        imageStore(uIndirectOutput, ivec2(threadIdx), vec4(vec3(0.0), 1.0));
        return;
    }
    uvec2 albedoMatId = texelFetch(uAlbedoMatId, ivec2(threadIdx), 0).rg;
    vec3 albedo = unpackAlbedo(albedoMatId.x);
    int matMeshId = int(albedoMatId.y);
    int matId = matMeshId >> 16;
    
    Ray ray = pinholeCameraSampleRay(uCamera, vec2(uv.x, 1.0 - uv.y), vec2(0));
    uint rng = makeSeed(uCamera.seed + threadIdx.x, threadIdx.y);

    vec3 radiance = vec3(0.0);
    vec3 throughput = vec3(1.0);
    vec3 throughputAfter = vec3(1.0);
    vec3 wo = -ray.dir;
    vec3 lastPos;
    bool isLastVertexConnectible = false;
    bool rcVertexFound = false;
    uint rcVertexId = 0;

    SurfaceInfo surf;
    surf.pos = ray.ori + ray.dir * (depth - 1e-4);
    surf.norm = norm;
    surf.albedo = albedo;
    surf.isLight = false;

    Material mat = uMaterials[matId];
    BSDFSample s;
    Intersection isec;

    GRISPathSample pathSample;
    GRISPathSampleReset(pathSample);

    pathSample.primaryHitRng = rng;

#pragma unroll
    for (int bounce = 0; bounce < MaxTracingDepth; bounce++) {
        GRISPathFlagsSetPathLength(pathSample.flags, bounce + 1);

        if (bounce > 0) {
            isec = rayQueryTraceClosestHit(
                uTLAS,
                gl_RayFlagsOpaqueEXT, 0xff,
                ray.ori, MinRayDistance, ray.dir, MaxRayDistance
            );

            if (!IntersectionIsValid(isec)) {
                break;
            }
            loadSurfaceInfo(isec, surf);
            mat = uMaterials[surf.matIndex];
        }

        float cosPrevWi = dot(ray.dir, surf.norm);
        float distToPrev = distance(lastPos, surf.pos);
        float geometryJacobian = abs(cosPrevWi) / square(distToPrev);

        if (surf.isLight) {
            if (/* cosPrevWi < 0 */ true) {
                float weight = 1.0;
                float sumPower = uLightSampleTable[0].prob;
                float lightPdf = luminance(surf.albedo) / sumPower / geometryJacobian;

                if (bounce > 0 && !isSampleTypeDelta(s.type)) {
                    weight = MISWeight(s.pdf, lightPdf);
                }
                if (rcVertexFound && bounce > rcVertexId) {
                    pathSample.rcVertexLi += surf.albedo * weight * throughputAfter;
                }
                radiance += surf.albedo * weight * throughput;
            }
        }
        bool isThisVertexConnectible = isBSDFConnectible(mat);

        if (!rcVertexFound && isLastVertexConnectible && isThisVertexConnectible && distToPrev > GRISDistanceThreshold) {
            rcVertexFound = true;

            pathSample.rcVertexIsec = isec;
            pathSample.rcVertexRng = rng;
            pathSample.rcPrevScatterPdf = s.pdf;
            pathSample.rcGeometryTerm = geometryJacobian;
            pathSample.rcVertexLi = surf.isLight ? surf.albedo : vec3(0.0);

            rcVertexId = bounce;
            GRISPathFlagsSetRcVertexId(pathSample.flags, rcVertexId);
        }

        if (surf.isLight) {
            break;
        }
        // make sure sample space is aligned for all paths
        vec4 lightRandSample = sample4f(rng);

        if (/* sample direct lighting */ bounce > 0 && !isBSDFDelta(mat)) {
            const uint shadowRayFlags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT;

            vec3 lightRadiance, lightDir;
            float lightDist, lightPdf;

            lightRadiance = sampleLight(surf.pos, lightDir, lightDist, lightPdf, lightRandSample);

            bool shadowed = rayQueryTraceShadow(
                uTLAS,
                shadowRayFlags, 0xff,
                surf.pos, MinRayDistance, lightDir, lightDist - 1e-4
            );

            if (!shadowed && lightPdf > 1e-6) {
                float bsdfPdf = absDot(surf.norm, lightDir) * PiInv;
                float weight = MISWeight(lightPdf, bsdfPdf);
                vec3 lightTerms = lightRadiance * evalBSDF(mat, surf.albedo, surf.norm, wo, lightDir) * satDot(surf.norm, lightDir) / lightPdf;
                radiance += lightTerms * weight * throughput;

                if (rcVertexFound) {
                    if (bounce == rcVertexId) {
                        pathSample.rcVertexLi = lightTerms * weight;
                        pathSample.rcVertexWi = lightDir;
                        pathSample.rcLightPdf = lightPdf;
                        GRISPathFlagsSetIsNEE(pathSample.flags, true);
                        break;
                    }
                    else {
                        pathSample.rcVertexLi += lightTerms * weight * throughputAfter;
                    }
                }
            }
        }
        bool skipRouletteForRcVertex = (rcVertexFound && (bounce == rcVertexId));

        if (/* russian roulette */ bounce > 4 && !skipRouletteForRcVertex) {
            float pdfTerminate = max(1.0 - luminance(throughput), 0);

            if (sample1f(rng) < pdfTerminate) {
                break;
            }
            throughput /= (1.0 - pdfTerminate);
        }

        if (!sampleBSDF(mat, surf.albedo, surf.norm, wo, sample3f(rng), s) || s.pdf < 1e-6) {
            break;
        }

        float cosTheta = isSampleTypeDelta(s.type) ? 1.0 : absDot(surf.norm, s.wi);
        vec3 scatterTerm = s.bsdf * cosTheta / s.pdf;
        throughput *= scatterTerm;

        if (rcVertexFound) {
            if (bounce == rcVertexId) {
                pathSample.rcVertexWi = s.wi;
                pathSample.rcScatterPdf = s.pdf;
            }
            else {
                throughputAfter *= scatterTerm;
            }
        }

        lastPos = surf.pos;
        wo = -s.wi;
        ray.dir = s.wi;
        ray.ori = surf.pos + ray.dir * 1e-4;
        isLastVertexConnectible = isThisVertexConnectible;
    }
    uGRISReservoir[index1D(threadIdx)].pathSample = pathSample;
    uint pathLength = GRISPathFlagsPathLength(pathSample.flags);

    //radiance = colorWheel(float(rcVertexId) / float(pathLength));
    //radiance = pathSample.rcVertexLi;
    //radiance = vec3(pathSample.rcScatterPdf);
    //radiance = vec3(pathSample.rcScatterPdf);
    //radiance = vec3(GRISPathFlagsRcVertexId(pathSample.flags) == 1);
    //radiance = vec3(pathSample.rcPrevScatterPdf);
    //radiance = vec3(pathSample.rcGeometryTerm);

    radiance = clampColor(radiance);
    float frameCount = float(uCamera.frameIndex & CameraFrameIndexMask);
    vec3 accumulatedRadaince = imageLoad(uIndirectOutput, ivec2(threadIdx)).rgb;
    accumulatedRadaince = (accumulatedRadaince * frameCount + radiance) / (frameCount + 1.0);

    imageStore(uIndirectOutput, ivec2(threadIdx), vec4(accumulatedRadaince, 1.0));
}